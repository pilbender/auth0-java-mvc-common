plugins {
    id 'java'
    id 'jacoco'
    id 'com.auth0.gradle.oss-library.java'
}

apply plugin: 'maven'
repositories {
    mavenLocal()
    mavenCentral()
}
jar {
    enabled = true
}

group 'com.auth0'

logger.lifecycle("Using version ${version} for ${name} group $group")

oss {
    name 'mvc-auth-commons'
    repository 'auth0-java-mvc-common'
    organization 'auth0'
    description 'Java library that simplifies the use of Auth0 for server-side MVC web apps'
    baselineCompareVersion '1.5.0'

    developers {
        auth0 {
            displayName = 'Auth0'
            email = 'oss@auth0.com'
        }
        lbalmaceda {
            displayName = 'Luciano Balmaceda'
            email = 'luciano.balmaceda@auth0.com'
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

test {
    useJUnitPlatform()
    testLogging {
        events "skipped", "failed"
        exceptionFormat "short"
    }
}

dependencies {
    implementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'com.google.guava:guava-annotations:r03'
    implementation 'commons-codec:commons-codec:1.15'

    api 'com.auth0:auth0:1.44.2'
    api 'com.auth0:java-jwt:3.19.4'
    api 'com.auth0:jwks-rsa:0.21.3'

    testImplementation 'org.bouncycastle:bcprov-jdk15on:1.64'
    testImplementation 'org.hamcrest:java-hamcrest:2.0.0.0'
    testImplementation 'org.hamcrest:hamcrest-core:1.3'
    testImplementation 'org.mockito:mockito-core:5.2.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.2'
    testImplementation 'org.springframework:spring-test:6.0.6'
    testImplementation 'com.squareup.okhttp3:okhttp:4.9.3'
    testImplementation 'jakarta.servlet:jakarta.servlet-api:6.0.0'
}

// Creates a version.txt file containing the current version of the SDK.
// This file is picked up and parsed by our Ship Orb to determine the version.
task exportVersion()  {
    doLast {
        new File(rootDir, "version.txt").text = "$version"
    }
}